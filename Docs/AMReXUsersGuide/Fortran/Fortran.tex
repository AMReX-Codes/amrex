The core of \amrex\ is written in \cpp.  For Fortran users who want to
write all of their programs in Fortran, \amrex\ provides Fortran
interfaces around most of functionalities except for 
the {\tt AmrLevel} class (Chapter~\ref{Chap:AmrLevel}).  
and particles (Chapter~\ref{Chap:Particles}) 
We should not confuse the Fortran
interface in this chapter with the Fortran kernel functions called
inside {\tt MFIter} loops in \cpp codes
(Section~\ref{sec:basics:fortran}).  For the latter, Fortran is used
in some sense as a domain-specific language with native
multi-dimensional arrays, whereas here Fortran is used to drive the
whole application code.  In order to better understand \amrex, Fortran
interface users should read the rest of the User's Guide except for
Chapters~\ref{Chap:AmrLevel} \& \ref{Chap:Particles}. 

\section{Getting Started}

We have discussed \amrex's build systems in
Chapter~\ref{Chap:BuildingAMReX}.  To build with GNU Make, we need to
include the Fortran interface source tree into the make system.  The
source codes for the Fortran interface are in {\tt
amrex/Src/F\_Interfaces} and there are several sub-directories.  The
{\tt Base} directory includes sources for the basic functionality, the
{\tt AmrCore} directory wraps around {\tt AmrCore} class
(Chapter~\ref{Chap:AmrCore}), and the {\tt Octree} adds support for
octree type of AMR grids.  Each directory has a {\tt Make.package}
file that can be included in make files (see {\tt
Tutorials/Basic/HelloWorld\_F} and {\tt Tutorials/Amr/Advection\_F}
for examples).  The {\tt libamrex} approach includes the Fortran
interface by default.  The CMake approach does not support the Fortran
interface yet.

A simple example can be found at {\tt Tutorials/Basic/HelloWorld\_F/}.
The source code is shown below in it entirety.  
\begin{lstlisting}[language=fortran]
subroutine amrex_fmain () bind(c)
  use amrex_base_module
  implicit none
  if (amrex_parallel_ioprocessor()) then
     print *, "Hello world!"
  end if
end subroutine amrex_fmain
\end{lstlisting}
Note that there is no {\tt program main} in the Fortran code.  The
{\tt main} function is actually in {\tt
  Src/F\_Interfaces/Base/AMReX\_fi\_main.cpp}.  That {\tt main}
function initializes and finalizes \amrex, and calls {\tt amrex\_fmain}
function in between.

To access \amrex\ Fortran interfaces, we can use these three
modules, {\tt amrex\_base\_module} for the basics functionalities
(Section~\ref{sec:fi:basics}), {\tt amrex\_amrcore\_module} for AMR
support (Section~\ref{sec:fi:amrcore}) and {\tt amrex\_octree\_module}
for octree style AMR (Section~\ref{sec:fi:octree}).

\section{The Basics}
\label{sec:fi:basics}

Module {\tt amrex\_base\_module} is a collection of various Fortran
modules providing interfaces to most of the basics of \amrex\ \cpp\
library {Chapter~\ref{Chap:Basics}).  These modules shown in this
section can be used without being explicitly included because they are
included by {\tt amrex\_base\_module}.

The spatial dimension is an integer parameter {\tt amrex\_spacedim}.
We can also use the {\tt AMREX\_SPACEDIM} macro in preprocessed
Fortran codes (e.g., {\tt .F90} files) just like in the \cpp\
codes.  Unlike in \cpp, the convention for \amrex\ Fortran interface
is coordinate direction index starts with 1.

There is an integer parameter {\tt amrex\_real}, a Fortran
kind parameter for {\tt real}.  Fortran {\tt real(amrex\_real)}
corresponds to {\tt amrex::Real} in \cpp, which is either double or
single precision depending the setting of precision.

Module {\tt amrex\_parallel\_module} ({\tt
  Src/F\_Interfaces/Base/AMReX\_parallel\_mod.F90}) includes wrappers
to the {\tt ParallelDescriptor} namespace, which is in turn a wrapper
to the parallel communication library used by \amrex\ (e.g. MPI).

Module {\tt amrex\_parmparse\_module} ({\tt
  Src/Base/AMReX\_parmparse\_mod.F90}) provides interface to {\tt
  ParmParse} (Section~\ref{sec:basics:parmparse}.  Here are some
examples.
\begin{lstlisting}[language=fortran]
  type(amrex_parmparse) :: pp
  integer :: n_cell, max_grid_size
  call amrex_parmparse_build(pp)
  call pp%get("n_cell", n_cell)
  max_grid_size = 32 ! default size
  call pp%query("max_grid_size", max_grid_size)
  call amrex_parmpase_destroy(pp) ! optional if compiler supports finalization
\end{lstlisting}
Finalization is a Fortran 2003 feature that some compilers may not
support.  For those compilers, we must explicitly destroy the objects,
otherwise there will be memory leaks.  This applies to many other
derived types.

{\tt amrex\_box} is a derived type in {\tt amrex\_box\_module} {\tt
  Src/F\_Interfaces/Base/AMReX\_box\_mod.F90}.  It has three members,
{\tt lo} (lower corner), {\tt hi} (upper corner) and {\tt nodal}
(logical flag for index type).  

{\tt amrex\_geometry} is a wrapper for the {\tt Geometry} class
containing information for the physical domain.  Below is an example
of building it.
\begin{lstlisting}[language=fortran]
  integer :: n_cell
  type(amrex_box) :: domain
  type(amrex_geometry) : geom
  ! n_cell = ...
  ! Define a single box covering the domain
  domain = amrex_box((/0,0,0/), (/n_cell-1, n_cell-1, n_cell-1/))
  ! This defines a amrex_geometry object.
  call amrex_geometry_build(geom, domain)
  !
  ! ...
  !
  call amrex_geometry_destroy(geom)
\end{lstlisting}


{\tt amrex\_boxarray} ({\tt
  Src/F\_Interfaces/Base/AMReX\_boxarray\_mod.F90}) is a wrapper for
the {\tt BoxArray} class, and {\tt amrex\_distromap} ({\tt
  Src/F\_Interfaces/Base/AMReX\_distromap\_mod.F90}) is a wrapper for
the {\tt DistributionMapping} class.  Here is an example of
building a {\tt BoxArray} and a {\tt DistributionMapping}.
\begin{lstlisting}[language=fortran]
  integer :: n_cell
  type(amrex_box) :: domain
  type(amrex_boxarray) : ba
  type(amrex_distromap) :: dm
  ! n_cell = ...
  ! Define a single box covering the domain
  domain = amrex_box((/0,0,0/), (/n_cell-1, n_cell-1, n_cell-1/))
  ! Initialize the boxarray "ba" from the single box "bx"
  call amrex_boxarray_build(ba, domain)
  ! Break up boxarray "ba" into chunks no larger than "max_grid_size"
  call ba%maxSize(max_grid_size)
  ! Build a DistributionMapping for the boxarray
  call amrex_distromap_build(dm, ba)
  !
  ! ...
  !
  call amrex_distromap_distromap(dm)
  call amrex_boxarray_destroy(ba)
\end{lstlisting}

Given {\tt amrex\_boxarray} and {\tt amrex\_distromap}, we can build
{\tt amrex\_multifab}, a wrapper for the {\tt MultiFab} class, as
follows. 
\begin{lstlisting}[language=fortran]
  integer :: ncomp, nghost
  type(amrex_boxarray) : ba
  type(amrex_distromap) :: dm
  type(amrex_multifab) :: mf, ndmf
  ! Build amrex_boxarray and amrex_distromap
  ! ncomp = ...
  ! nghost = ...
  ! ...
  ! Build amrex_multifab with ncomp component and nghost ghost cells
  call amrex_multifab_build(mf, ba, dm, ncomp, nghost)
  ! Build a nodal multifab
  call amrex_multifab_build(ndmf,ba,dm,ncomp,nghost,(/.true.,.true.,.true./))
  !
  ! ...
  !
  call amrex_multifab_destroy(mf)
  call amrex_multifab_destroy(ndmf)
\end{lstlisting}
There are many type-bound procedures for {\tt amrex\_multifab}.  For
example
\begin{lstlisting}[language=fortran]
  ncomp   ! Return the number of components
  nghost  ! Return the number of ghost cells
  setval  ! Set the data to the given value 
  copy    ! Copy data from given amrex_multifab to this amrex_multifab
\end{lstlisting}
Note that the {\tt copy} function here only works on copying data from
another {\tt amrex\_multifab} built with the same {\tt
  amrex\_distromap}, like the {\tt MultiFab::Copy} function in \cpp.
{\tt amrex\_multifab} also has two parallel communication procedures,
{\tt fill\_boundary} and {\tt parallel\_copy}.  Their and interface
and usage are very similar to functions {\tt FillBoundary} and {\tt
  ParallelCopy} for {\tt MultiFab} in \cpp.
\begin{lstlisting}[language=fortran]
  type(amrex_geometry) :: geom
  type(amrex_multifab) :: mf, mfsrc
  ! ...
  call mf%fill_boundary(geom)       ! Fill all components
  call mf%fill_boundary(geom, 1, 3) ! Fill 3 components starting with component 1

  call mf%parallel_copy(mfsrc, geom) ! Parallel copy from another multifab
\end{lstlisting}
It should be emphasized that the component index for {\tt
  amrex\_multifab} starts with 1 following Fortran convention.  This
is different from \cpp\ part of \amrex.

\amrex\ provides a Fortran interface to {\tt MFIter} for iterating
over the data in {\tt amrex\_multifab}.  The Fortran type for this is
{\tt amrex\_mfiter}.  Here is an example of using {\tt amrex\_mfiter}
to loop over {\tt amrex\_multifab} with tiling and launch a kernel
function.
\begin{lstlisting}[language=fortran]
  integer :: plo(4), phi(4)
  type(amrex_box) :: bx
  real(amrex_real), contiguous, dimension(:,:,:,:), pointer :: po, pn
  type(amrex_multifab) :: old_phi, new_phi
  type(amrex_mfiter) :: mfi
  ! Define old_phi and new_phi ...
  ! In this example they are built with the same boxarray and distromap.
  ! And they have the same number of ghost cells and 1 component.
  call amrex_mfiter_build(mfi, old_phi, tiling=.true.)
  do while (mfi%next())
    bx = mfi%tilebox()
    po => old_phi%dataptr(mfi)
    pn => new_phi%dataptr(mfi)
    plo = lbound(po)
    phi = ubound(po)
    call update_phi(bx%lo, bx&hi, po, pn, plo, phi)
  end do
  call amrex_mfiter_destroy(mfi)
\end{lstlisting}
Here procedure {\tt update\_phi} is
\begin{lstlisting}[language=fortran]
 subroutine update_phi (lo, hi, pold, pnew, plo, phi)
  integer, intent(in) :: lo(3), hi(3), plo(3), phi(3)
   real(amrex_real),intent(in   ) pold(plo(1):phi(1),plo(2):phi(2),plo(3):phi(3))
   real(amrex_real),intent(inout) pnew(plo(1):phi(1),plo(2):phi(2),plo(3):phi(3))
   ! ...
 end subroutine update_phi
\end{lstlisting}
Note that {\tt amrex\multifab}'s procedure {\tt dataptr} takes {\tt
  amrex\_mfiter} and returns a 4-dimensional Fortran pointer.  For
performance, we should declare the pointer as {\tt contiguous}.  In
\cpp, the similar operation returns a reference to {\tt FArrayBox}.
However, {\tt FArrayBox} and Fortran pointer have a similar capability
of containing array bound information.  We can call {\tt lbound} and
{\tt ubound} on the pointer to return its lower and upper bounds.  The
first three dimensions of the bounds are spatial and the fourth is for
the number of component.

Many of the derived Fortran types in \amrex\ (e.g., {\tt
  amrex\_multifab}, {\tt amrex\_boxarray}, {\tt amrex\_distromap},
{\tt amrex\_mfiter}, and {\tt amrex\_geometry}) contain a {\tt
  type(c\_ptr)} that points a \cpp\ object.  They also contain a
logical type indicating whether or not this object owns the underlying
object (i.e., responsible for deleting the object).  Due to the
semantics of Fortran, one should not return these types with
functions.  Instead we should pass them as arguments to procedures
(preferably with {\tt intent} specified).  These five types all have
{\tt assignment(=)} operator that performs a shallow copy.  After the
assignment, the original objects still owns the data and the copy is
just an alias.  For example,
\begin{lstlisting}[language=fortran]
  type(amrex_multifab) :: mf1, mf2
  call amrex_multifab_build(mf1, ...)
  call amrex_multifab_build(mf2, ...)
  ! At this point, both mf1 and mf2 are data owners
  mf2 = mf1   ! This will destroy the original data in mf2.
              ! Then mf2 becomes a shallow copy of mf1.
              ! mf1 is still the owner of the data.
  call amrex_multifab_destroy(mf1)
  ! mf2 no longer contains a valid pointer because mf1 has been destroyed. 
  call amrex_multifab_destroyed(mf2)  ! But we still need to destroy it.
\end{lstlisting}
If we need to transfer the ownership, {\tt amrex\_multifab}, {\tt
  amrex\_boxarray} and {\tt amrex\_distromap} provide type-bound {\tt
  move} procedure.  We can use it as follows
\begin{lstlisting}[language=fortran]
  type(amrex_multifab) :: mf1, mf2
  call amrex_multifab_build(mf1, ...)
  call mf2%move(mf1)   ! mf2 is now the data owner and mf1 is not.
  call amrex_multifab_destroy(mf1)
  call amrex_multifab_destroyed(mf2)
\end{lstlisting}
{\tt amrex\_multifab} also has a type-bound {\tt swap} procedure for
exchanging the data.

\amrex\ also provides {\tt amrex\_plotfile\_module} for writing
plotfiles.  The interface is similar to the \cpp versions.

\section{Amr Core Infrastructure}
\label{sec:fi:amrcore}

Module {\tt amrex\_amr\_module} provides interfaces to AMR core
infrastructure.  With AMR, subroutine {\tt amrex\_fmain} should look
like below,
\begin{lstlisting}[language=fortran]
  subroutine amrex_fmain () bind(c)
    use amrex_amr_module
    implicit none  
    call amrex_amrcore_init()
    call my_amr_init()       ! user's own code, not part of AMReX
    ! ...
    call my_amr_finalize()   ! user's own code, not part of AMReX
    call amrex_amrcore_finalize()
  end subroutine amrex_fmain
\end{lstlisting}
Here we need to call {\tt amrex\_amrcore\_init} and {\tt
  amrex\_amrcore\_finalize}.  And usually we need to call application
code specific procedures to provide some ``hooks'' needed by \amrex.
In \cpp, this is achieved by using virtual functions.  In Fortran, we
need to call
\begin{lstlisting}[language=fortran]
  subroutine amrex_init_virtual_functions (mk_lev_scrtch, mk_lev_crse, &
                                           mk_lev_re, clr_lev, err_est)

    ! Make a new level from scratch using provided boxarray and distromap
    ! Only used during initialization.
    procedure(amrex_make_level_proc)  :: mk_lev_scrtch
    ! Make a new level using provided boxarray and distromap, and fill
    ! with interpolated coarse level data.
    procedure(amrex_make_level_proc)  :: mk_lev_crse
    ! Remake an existing level using provided boxarray and distromap,
    ! and fill with existing fine and coarse data.
    procedure(amrex_make_level_proc)  :: mk_lev_re
    ! Delete level data
    procedure(amrex_clear_level_proc) :: clr_lev
    ! Tag cells for refinement
    procedure(amrex_error_est_proc)   :: err_est
  end subroutine amrex_init_virtual_functions
\end{lstlisting}
We need to provide five functions and these functions have three types
of interfaces:
\begin{lstlisting}[language=fortran]
  subroutine amrex_make_level_proc (lev, time, ba, dm) bind(c)
    import
    implicit none
    integer, intent(in), value :: lev
    real(amrex_real), intent(in), value :: time
    type(c_ptr), intent(in), value :: ba, dm
  end subroutine amrex_make_level_proc
  
  subroutine amrex_clear_level_proc (lev) bind(c)
    import
    implicit none
    integer, intent(in) , value :: lev
  end subroutine amrex_clear_level_proc
  
  subroutine amrex_error_est_proc (lev, tags, time, tagval, clearval) bind(c)
    import
    implicit none
    integer, intent(in), value :: lev
    type(c_ptr), intent(in), value :: tags
    real(amrex_real), intent(in), value :: time
    character(c_char), intent(in), value :: tagval, clearval
  end subroutine amrex_error_est_proc
\end{lstlisting}
{\tt Tutorials/Amr/Advection\_F/Source/my\_amr\_mod.F90} shows an
example of the setup process.  The user provided {\tt
  procedure(amrex\_error\_est\_proc)} has a {\tt tags} argument that
is of type {\tt c\_ptr} and its value is a pointer to a \cpp\ {\tt
  TagBoxArray} object.  We need to convert this into a Fortran {\tt
  amrex\_tagboxarray} object.
\begin{lstlisting}[language=fortran]
  type(amrex_tagboxarray) :: tag
  tag = tags
\end{lstlisting}

Module {\tt amrex\_fillpatch\_module} provides interface to
\cpp\ functions {\tt FillPatchSinglelevel} and {\tt
  FillPatchTwoLevels}.   To use it, the application code needs to
provide procedures for interpolation and filling physical boundaries.
See {\tt Tutorials/Amr/Advection\_F/Source/fillpatch\_mod.F90} for an
example. 

Module {\tt amrex\_fluxregister\_module} provides interface to {\tt
  FluxRegister} (Section~\ref{sec:amrcore:fluxreg}).  Its usage is
demonstrated in the tutorial at {\tt
  Tutorials/Amr/Advection\_F/}. 

\section{Octree}
\label{sec:fi:octree}

In \amrex, the union of fine level grids is properly contained within
the union of coarse level grids.  There are no required direct
parent-child connections between levels.  Therefore, grids in \amrex\
in general cannot be represented by trees.  Nevertheless, octree type
grids are supported via Fortran interface, because \amrex\ grids are
more general than octree grids.  A tutorial example using {\tt
  amrex\_octree\_module} ({\tt
  Src/F\_Interfaces/Octree/AMReX\_octree\_mod.f90}) is available at
{\tt Tutorials/Amr/Advection\_octree\_F/}.  Procedures {\tt
  amrex\_octree\_init} and {\tt amrex\_octree\_finalize} must be
called as follows,
\begin{lstlisting}[language=fortran]
  subroutine amrex_fmain () bind(c)
    use amrex_amrcore_module
    use amrex_octree_module
    implicit none
    call amrex_octree_int()  ! This should be called first.
    call amrex_amrcore_init()
    call my_amr_init()       ! user's own code, not part of AMReX
    ! ...
    call my_amr_finalize()   ! user's own code, not part of AMReX
    call amrex_amrcore_finalize()
    call amrex_octree_finalize()
  end subroutine amrex_fmain
\end{lstlisting}
By default, the grid size is $8^3$, and this can be changed via {\tt
  ParmParse} parameter {\tt amr.max\_grid\_size}.  Module {\tt
  amrex\_octree\_module} provides {\tt amrex\_octree\_iter} that can
be used to iterate over leaves of octree.  For example,
\begin{lstlisting}[language=fortran]
  type(amrex_octree_iter) :: oti
  type(multifab) :: phi_new(*)   ! one multifab for each level
  integer :: ilev, igrd
  type(amrex_box) :: bx
  real(amrex_real), contiguous, pointer, dimension(:,:,:,:) :: pout
  call amrex_octree_iter_build(oti)
  do while(oti%next())
     ilev = oti%level()
     igrd = oti%grid_index()
     bx   = oti%box()
     pout => phi_new(ilev)%dataptr(igrd)
     ! ...
  end do
  call amrex_octree_iter_destroy(oti)
\end{lstlisting}
